buildscript {
    repositories {
        mavenCentral()
        maven { url 'https://maven.fabric.io/public' }
    }

    dependencies {
        classpath 'io.fabric.tools:gradle:1.24.4'
    }
}

apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'
apply plugin: 'kotlin-kapt'
apply plugin: 'io.fabric'

repositories {
    maven { url 'https://maven.fabric.io/public' }
    mavenCentral()
}

android {
    compileSdkVersion 26
    buildToolsVersion '26.0.2'

    defaultConfig {
        applicationId "com.sharpdroid.registroelettronico"
        minSdkVersion 15
        targetSdkVersion 26
        versionCode 504
        versionName "2.1.04"

        vectorDrawables.useSupportLibrary = true
        multiDexEnabled true

        javaCompileOptions {
            annotationProcessorOptions {
                arguments = ["room.schemaLocation": "$projectDir/schemas".toString()]
            }
        }
    }

    dexOptions {
        javaMaxHeapSize "2g"
    }

    dataBinding {
        enabled true
    }

    compileOptions {
        sourceCompatibility = JavaVersion.VERSION_1_8
        targetCompatibility = JavaVersion.VERSION_1_8
    }

    signingConfigs {
        config {
            if (rootProject.file("keystore.properties").exists()) {
                def keystorePropertiesFile = rootProject.file("keystore.properties")
                def keystoreProperties = new Properties()
                keystoreProperties.load(new FileInputStream(keystorePropertiesFile))

                keyAlias keystoreProperties['keyAlias']
                keyPassword keystoreProperties['keyPassword']
                storeFile file(keystoreProperties['storeFile'])
                storePassword keystoreProperties['storePassword']
            }

            return void
        }
    }

    buildTypes {
        debug {
            debuggable true

            if (rootProject.file("keystore.properties").exists()) {
                signingConfig signingConfigs.config
            }

            // Disable fabric build ID generation for debug builds
            ext.enableCrashlytics = false

            return void
        }

        release {
            debuggable false

            zipAlignEnabled true
            shrinkResources false
            minifyEnabled false
            useProguard false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'

            if (rootProject.file("keystore.properties").exists()) {
                signingConfig signingConfigs.config
            }

            return void
        }
    }

    return void
}

allprojects {
    repositories {
        maven {
            url 'https://jitpack.io'
        }
        maven {
            url 'https://dl.bintray.com/vsvankhede/maven/'
        }
        google()
    }
}

dependencies {
    implementation fileTree(include: ['*.jar'], dir: 'libs')
    implementation('com.crashlytics.sdk.android:crashlytics:2.7.1@aar') {
        transitive = true
    }
    implementation ('com.google.firebase:firebase-core:11.6.0'){
        exclude module: 'firebase-database'
    }
    implementation('com.crashlytics.sdk.android:answers:1.4.1@aar') {
        transitive = true
    }
    implementation('com.mikepenz:materialdrawer:5.9.5@aar') {
        transitive = true
    }
    implementation('com.mikepenz:aboutlibraries:5.9.7@aar') {
        transitive = true
    }

    kapt 'com.android.databinding:compiler:3.0.1'

    implementation 'com.afollestad.material-dialogs:core:0.9.4.7'
    implementation 'com.akexorcist:RoundCornerProgressBar:2.0.3'
    implementation 'com.andkulikov:transitionseverywhere:1.7.6'
    implementation 'com.android.support:appcompat-v7:26.1.0'
    implementation 'com.android.support:cardview-v7:26.1.0'
    implementation 'com.android.support:design:26.1.0'
    implementation 'com.android.support:preference-v14:26.1.0'
    implementation 'com.android.support:preference-v7:26.1.0'
    implementation 'com.android.support:recyclerview-v7:26.1.0'
    implementation 'com.android.support:support-v13:26.1.0'
    implementation 'com.android.support:support-v4:26.1.0'
    implementation 'com.github.clans:fab:1.6.4'
    implementation 'com.github.PhilJay:MPAndroidChart:v3.0.2'
    implementation 'com.github.sundeepk:compact-calendar-view:2.0.2.2'
    implementation 'com.google.code.gson:gson:2.8.2'
    implementation 'com.heinrichreimersoftware:material-intro:1.6.2'
    implementation 'com.squareup.retrofit2:adapter-rxjava2:2.3.0'
    implementation 'com.squareup.retrofit2:converter-gson:2.3.0'
    implementation 'com.squareup.retrofit2:retrofit:2.3.0'
    implementation 'com.wdullaer:materialdatetimepicker:3.3.1'
    implementation 'com.yqritc:recyclerview-flexibledivider:1.4.0'
    implementation 'de.hdodenhof:circleimageview:2.2.0'
    implementation 'io.reactivex.rxjava2:rxandroid:2.0.1'
    implementation 'io.reactivex.rxjava2:rxjava:2.1.6'
    implementation 'org.apache.commons:commons-lang3:3.6'

    //room
    implementation "android.arch.persistence.room:runtime:1.0.0"
    implementation "android.arch.persistence.room:rxjava2:1.0.0"
    kapt "android.arch.persistence.room:compiler:1.0.0"

    //LiveData & ViewModel
    implementation "android.arch.lifecycle:extensions:1.0.0"

    //For Lifecycles Java8 Lanaguage support, add:
    implementation "android.arch.lifecycle:common-java8:1.0.0"

    implementation 'com.google.firebase:firebase-messaging:11.6.0'
    implementation 'com.firebase:firebase-jobdispatcher:0.8.4'

    implementation "org.jetbrains.kotlin:kotlin-stdlib-jre8:$kotlin_version"

    implementation 'com.android.support:multidex:1.0.2'
}

apply plugin: 'com.google.gms.google-services'
